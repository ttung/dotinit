if ($?f_gatherpaths) then
    set C_PATH
    set C_MANPATH
    set C_LD_LIBRARY_PATH
    set C_INCLUDE
    set C_CLASSPATH

    # initialize path variables for all prefixed subsystem
    foreach prefix (/sw /usr/local /opt/local /usr/openwin /usr/dt /usr/ccs /usr/X11 /usr/X11R6 /usr/contrib)
        set C_PATH		= (${C_PATH} ${prefix}/bin ${prefix}/sbin)

        set C_MANPATH		= (${C_MANPATH} ${prefix}/man ${prefix}/share/man)

        set C_LD_LIBRARY_PATH	= (${C_LD_LIBRARY_PATH} ${prefix}/lib)

#        set C_INCLUDE		= (${C_INCLUDE} ${prefix}/include)
    end
    cstat "."

    # miscellaneous paths
    set C_PATH			= (${C_PATH} /usr/bin /bin /usr/sbin /sbin /etc \
                                    /usr/ucb /usr/etc /usr/bin/X11)
    set C_MANPATH		= (${C_MANPATH} /usr/man /usr/share/man)
    set C_LD_LIBRARY_PATH	= (${C_LD_LIBRARY_PATH} /lib /usr/lib /usr/lib/X11)
#    set C_INCLUDE		= (${C_INCLUDE} /usr/include /usr/include/X11)
    cstat "."

    if ($OSTYPE == solaris) then
        set C_PATH		= (${C_PATH} /lang/SUNWspro/bin /opt/SUNWspro/bin /opt/SUNWdtpcv/bin)

        set C_MANPATH		= (${C_MANPATH} /lang/SUNWspro/man /opt/SUNWspro/man)

        set C_LD_LIBRARY_PATH	= (${C_LD_LIBRARY_PATH} /lang/SUNWspro/lib /opt/SUNWspro/lib /opt/SUNWdtpcv/lib)
    endif

    # initialize path variables for HOME subsystem.
    if ($?HOME) then
        set C_PATH		= (${HOME}/software/bin ${HOME}/software/bin-${MACHTYPE}-${OSTYPE} ${C_PATH})
    
        set C_LD_LIBRARY_PATH	= (${HOME}/software/lib ${HOME}/software/lib-${MACHTYPE}-${OSTYPE} ${C_LD_LIBRARY_PATH})

        set C_MANPATH		= (${HOME}/software/man ${HOME}/software/man-${MACHTYPE}-${OSTYPE} ${C_MANPATH})

        set C_INCLUDE		= (${HOME}/software/include ${HOME}/software/include-${MACHTYPE}-${OSTYPE} ${C_INCLUDE})

        set C_CLASSPATH		= (${HOME}/software/classes ${C_CLASSPATH})
    endif
    cstat "."
endif

# even if we are just gathering, we should probably set the paths, in case
# later init files require things that are in the path.  we just have to set it 
# twice.
if ($?f_setpaths) then
    setenv PATH .
    setenv MANPATH .
    setenv LD_LIBRARY_PATH  .
    setenv INCLUDE .
    setenv CLASSPATH .

    setenv GCC_DFLAG ""
    setenv GCC_IFLAG ""
    setenv GCC_LFLAG ""

    foreach bin (${C_PATH})
        if (-d ${bin}) then
            setenv PATH			"${PATH}:${bin}"
        endif
    end
    cstat "."
    foreach man (${C_MANPATH})
        if (-d ${man}) then
            setenv MANPATH		"${MANPATH}:${man}"
        endif
    end
    cstat "."
    foreach lib (${C_LD_LIBRARY_PATH})
        if (-d ${lib}) then
            setenv LD_LIBRARY_PATH	"${LD_LIBRARY_PATH}:${lib}"
            setenv GCC_LFLAG		"${GCC_LFLAG} -L${lib}"
        endif
    end
    cstat "."
    foreach include (${C_INCLUDE})
        if (-d ${include}) then
            setenv INCLUDE		"${INCLUDE}:${include}"
            setenv GCC_IFLAG		"${GCC_IFLAG} -I${include}"
        endif
    end
    cstat "."

    setenv GCC_LFLAG		"${GCC_LFLAG} -L."
    setenv GCC_IFLAG		"${GCC_IFLAG} -I."

    if (! $?f_gatherpaths) then
        unset C_PATH C_MANPATH C_LD_LIBRARY_PATH C_INCLUDE C_CLASSPATH
    endif

    cstat "done\n"
endif
