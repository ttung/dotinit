if ($?cp_version) then
    alias ver			echo "cshrc package" $cp_version
endif

# basic unix stuff
alias rm			rm -i
alias cp			cp -i
alias mv			mv -i

alias l				ls -F
alias ls			ls -F
alias ll			ls -l
alias lla			ls -al

alias dirs                      dirs -v
alias pushd                     pushd -v
alias popd                      popd -v


# process control
alias ups			ps -U $USER
alias j				jobs
if ($OSTYPE == Linux) then
    alias top			nice +2 top d 1
else
    alias top			nice +2 top -s 1
endif
alias pgrep                     pgrep -f

# pagers
alias more			less -meiR

# other cmd line utilities
alias cvsdiff			"cvs status \!* |& egrep 'Loc|Merg|conflicts|cvs status'"
alias cvsstat			"cvs status \!* |& egrep 'Loc|Nee|conflicts|Unknown|cvs status'"
if ($?tcsh) then
    if (-X gmake) then
        alias make		gmake
    endif
endif

if (-x ${HOME}/software/bin/mcal) then
    alias cal			mcal
endif
if (-x ${HOME}/software/bin/mscreen) then
    alias screen		mscreen
endif

if (-e /bin/umask) then
    alias umask			"if (\!* == "\"\"") /bin/umask -S ; if (\!* != "\"\"") umask \!*"
else if (-e /usr/bin/umask) then
    alias umask			"if (\!* == "\"\"") /usr/bin/umask -S ; if (\!* != "\"\"") umask \!*"
endif


# editors
alias en			emacs -nw
alias em			emacs

# x stuff
alias xload			xload -bg black -fg white -update 1 -jumpscroll 1 -hl green

if ($?USER && $?HOST) then
    alias xterm			'xterm -ls -T '${USER}@${HOST}' -geometry 82x25'
    alias bigxterm		'xterm -ls -T '${USER}@${HOST}' -geometry 82x25'
endif

if ($OSTYPE == solaris) then
    alias asdf			xmodmap .dvorak.keysym
    alias aoeu			echo "Cannot revert keysyms"
else
    alias asdf			~/.xmodmap ~/.dvorak \| xmodmap -
    alias aoeu			~/.xmodmap ~/.qwerty \| xmodmap -
endif
alias unswap_caps 'xmodmap -e "remove lock = Control_L" -e "remove control = Caps_Lock" -e "add lock = Caps_Lock" -e "add control = Control_L"'
alias swap_caps 'xmodmap -e "remove lock = Caps_Lock" -e "remove control = Control_L" -e "add lock = Control_L" -e "add control = Caps_Lock"'

# conveniences
alias motd			less -m -e /etc/motd
alias f				finger

if ($?HOME) then
    alias rp			'alias cstat "echo \!* > /dev/null"; set f_gatherpaths f_setpaths; source '${HOME}'/.cshrc.paths; unset f_gatherpaths; source '${HOME}'/.cshrc.paths; unset f_setpaths; unalias cstat'
endif

cstat "done\n"
