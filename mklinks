#!/bin/tcsh
cd ${HOME}

if ($argv == "clean") then
    set clean
endif

if ($user == "cwu") then
    set addr_user = iyaowu
else if ($user == "tungc") then
    set addr_user = ctung
else
    set addr_user = ${USER}
endif

if (-d .init/${addr_user}-addressbook/) then
    foreach addrbook (.init/${addr_user}-addressbook/addr*)
        set addrbookdest = `echo $addrbook | sed -e 's/.*\/\([^\/]*\)/.\1/'`

        if (-e ${addrbookdest}) then
            if (! -l ${addrbookdest}) then
                set skiplink
                echo "Warning: ${addrbookdest} is in the way."
            endif

            if ($?clean || -l ${addrbookdest}) then
                unset skiplink
                'rm' -f ${addrbookdest}
            endif
        endif

        if ($?skiplink) then
            unset skiplink
        else
            ln -sf ${addrbook} ${addrbookdest}
        endif

    end
endif

foreach addrbook (.address*[^\.][^l][^u])
    if (-e $addrbook && ! -l $addrbook) then
        echo "Warning: $addrbook may need to be checked into CVS."
        if ($?clean) then
            set addrbookdest = `echo $addrbook | sed -e 's/\.\(.*\)/\1/'`
            mkdir -p .init/${addr_user}-addressbook/
            'mv' -f ${addrbook} .init/${addr_user}-addressbook/${addrbookdest}
            ln -s f .init/${addr_user}-addressbook/${addrbookdest} ${addrbook}
        endif
    endif
end

foreach cshrc (.init/cshrc*)
    set cshrcdest = `echo ${cshrc} | sed -e 's/.*\/\([^\/]*\)/.\1/'`
    if (-e ${cshrcdest}) then
        if (! -l ${cshrcdest}) then
            set skiplink
            echo "Warning: ${cshrcdest} is in the way."
        endif

        if ($?clean || -l ${cshrcdest}) then
            unset skiplink
            'rm' -f ${cshrcdest}
        endif
    endif

    if ($?skiplink) then
        unset skiplink
    else
        ln -sf ${cshrc} ${cshrcdest}
    endif
end

foreach df ( emacs xstart dvorak xsession qwerty fvwm2rc xmodmap \
             dvorak.keysym Xdefaults hushlogin screenrc )
    if (-e .${df}) then
        if (! -l .${df}) then
            set skiplink
            echo "Warning: .${df} is in the way."
        endif

        if ($?clean || -l .${df}) then
            unset skiplink
            'rm' -f .${df}
        endif
    endif

    if ($?skiplink) then
        unset skiplink
    else
        ln -sf .init/${df} .${df}
    endif
end

set src = ( .cshrc.aliases .cshrc.login .cshrc.logout .xsession )
set dst = (       .aliases       .login       .logout .xinitrc  )

set cntr = 1
while ($cntr <= ${#src})
    if (-e ${dst[$cntr]}) then
        if (! -l ${dst[$cntr]}) then
            set skiplink
            echo "Warning: ${dst[$cntr]} is in the way."
        endif

        if ($?clean || -l ${dst[$cntr]}) then
            'rm' -f ${dst[$cntr]}
        endif
    endif

    if ($?skiplink) then
        unset skiplink
    else
        ln -sf ${src[$cntr]} ${dst[$cntr]}
    endif

    set cntr = `expr ${cntr} + 1`
end

foreach dotfile (.*)
    if (-l $dotfile && ! -e $dotfile) then
        echo "Warning: $dotfile is a broken link."
        if ($?clean) then
            'rm' -f ${dotfile}
        endif
    endif
end

cd -
