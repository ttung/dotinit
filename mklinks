#!/bin/tcsh
cd ${HOME}

if ($argv == "clean") then
    set clean
endif

if ($user == "cwu") then
    set addr_user = iyaowu
else if ($user == "tungc") then
    set addr_user = ctung
else
    set addr_user = ${USER}
endif

echo "Addressbook..."
if (-d .init/${addr_user}-addressbook/) then
    foreach addrbook (.init/${addr_user}-addressbook/addr*)
        set addrbookdest = `echo $addrbook | sed -e 's/.*\/\([^\/]*\)/.\1/'`

        if (-e ${addrbookdest}) then
            if (! -l ${addrbookdest}) then
                set skiplink
                echo "Warning: ${addrbookdest} is in the way and may need to be checked into CVS."
                if ($?clean) then
                    unset skiplink
                    mkdir -p .init/${addr_user}-addressbook/
                    'mv' -f ${addrbookdest} ${addrbook}
                    echo "Cleaning by moving ${addrbookdest} to ${addrbook}."
                endif
            else
                'rm' -f ${addrbookdest}
            endif
        endif

        if ($?skiplink) then
            unset skiplink
        else
            ln -sf ${addrbook} ${addrbookdest}
        endif

    end
endif

echo "cshrc..."
foreach cshrc (.init/cshrc*)
    set cshrcdest = `echo ${cshrc} | sed -e 's/.*\/\([^\/]*\)/.\1/'`
    if (-e ${cshrcdest}) then
        if (! -l ${cshrcdest}) then
            set skiplink
            echo "Warning: ${cshrcdest} is in the way and may need to be checked into CVS."
            if ($?clean) then
                unset skiplink
                'mv' -f ${cshrcdest} ${cshrc}
                echo "Cleaning by moving ${cshrcdest} to ${cshrc}."
            endif            
        else
            unset skiplink
            'rm' -f ${cshrcdest}
        endif
    endif

    if ($?skiplink) then
        unset skiplink
    else
        ln -sf ${cshrc} ${cshrcdest}
    endif
end

set fileset = ( xstart dvorak xsession qwerty fvwm2rc xmodmap \
             dvorak.keysym Xdefaults hushlogin screenrc )

echo "Miscellaneous..."
foreach df ( ${fileset} )
    set dfdest = .${df}
    if (-e ${dfdest}) then
        if (! -l ${dfdest}) then
            set skiplink
            echo "Warning: ${dfdest} is in the way and may need to be checked into CVS."
            if ($?clean) then
                unset skiplink
                'mv' -f ${dfdest} .init/${df}
                echo "Cleaning by moving ${dfdest} to .init/${df}."
            endif
        else
            'rm' -f ${dfdest}
        endif
    endif

    if ($?skiplink) then
        unset skiplink
    else
        ln -sf .init/${df} ${dfdest}
    endif
end

set src = ( .cshrc.aliases .cshrc.login .cshrc.logout .xsession )
set dst = (       .aliases       .login       .logout .xinitrc  )

set cntr = 1
while ($cntr <= ${#src})
    if (-e ${dst[$cntr]}) then
        if (! -l ${dst[$cntr]}) then
            set skiplink
            echo "Warning: ${dst[$cntr]} is in the way."
        endif

        if ($?clean || -l ${dst[$cntr]}) then
            'rm' -f ${dst[$cntr]}
        endif
    endif

    if ($?skiplink) then
        unset skiplink
    else
        ln -sf ${src[$cntr]} ${dst[$cntr]}
    endif

    set cntr = `expr ${cntr} + 1`
end

foreach dotfile (.*)
    if (-l $dotfile && ! -e $dotfile) then
        echo "Warning: $dotfile is a broken link."
        if ($?clean) then
            'rm' -f ${dotfile}
        endif
    endif
end

if (-e ${HOME}/emacs) then
    make -C ${HOME}/emacs
endif

cd -
