#!/bin/tcsh
cd ${HOME}

if ($user == "cwu") then
    set addr_user = iyaowu
else if ($user == "tungc") then
    set addr_user = ctung
else
    set addr_user = tonytung
endif

foreach dotfile (.*)
    if (-l $dotfile && ! -e $dotfile) then
        echo "Warning: $dotfile is a broken link."
    endif
end

set nonomatch
foreach addrbook (.address*[^\.][^l][^u])
    set addrbookdest = `echo $addrbook | sed -e 's/\.\(.*\)/\1/'`
    if (! -l $addrbook) then
        mkdir -p .init/${addr_user}-addressbook/
        'mv' -f $addrbook .init/${addr_user}-addressbook/${addrbookdest}
    else
        'rm' -f $addrbook
    endif
end

foreach cshrc (.init/cshrc*)
    set cshrcdest = `echo ${cshrc} | sed -e 's/.*\/\([^\/]*\)/.\1/'`
    if (-e ${cshrcdest}) then
        if (-l ${cshrcdest}) then
            # it's a link, we can remove it
            'rm' -f ${cshrcdest}
        else
            set skiplink
            echo "Warning: ${cshrcdest} is in the way."
        endif
    endif

    if ($?skiplink) then
        unset skiplink
    else
        ln -sf ${cshrc} ${cshrcdest}
    endif
end

foreach df ( emacs xstart dvorak xsession qwerty fvwm2rc xmodmap \
             dvorak.keysym Xdefaults hushlogin screenrc )
    if (-e .${df}) then
        if (-l .${df}) then
            'rm' -f .${df}
        else
            set skiplink
            echo "Warning: .${df} is in the way."
        endif
    endif

    if ($?skiplink) then
        unset skiplink
    else
        ln -sf .init/${df} .${df}
    endif
end

set src = ( .cshrc.aliases .cshrc.login .cshrc.logout .xsession )
set dst = (       .aliases       .login       .logout .xinitrc  )

set cntr = 1
while ($cntr <= ${#src})
    if (-e ${dst[$cntr]}) then
        if (-l ${dst[$cntr]}) then
            'rm' -f ${dst[$cntr]}
        else
            set skiplink
            echo "Warning: ${dst[$cntr]} is in the way."
        endif
    endif

    if ($?skiplink) then
        unset skiplink
    else
        ln -sf ${src[$cntr]} ${dst[$cntr]}
    endif

    set cntr = `expr ${cntr} + 1`
end

if (-d .init/${addr_user}-addressbook/) then
    foreach addrbook (.init/${addr_user}-addressbook/addr*)
        set addrbookdest = `echo $addrbook | sed -e 's/.*\/\([^\/]*\)/.\1/'`
        ln -sf $addrbook $addrbookdest
    end
endif

cd -
