set rmalias=`alias rm`
unalias rm

rm -f ~/.pine-debug* >& /dev/null
rm -f ~/dead.letter
rm -f ~/.saves-* >& /dev/null

alias rm $rmalias
unset rmalias

stty erase '^H' kill '^U' intr '^C'

if (! $?EMACS ) then
    set noglob
        eval `tset -s - | sed -e '1d'`
    unset noglob
    mesg y
else if ($TERM == "eterm") then
    set noglob
        eval `tset -s - vt100 | sed -e '1d'`
    unset noglob
else
    
endif

if ($?TERMCAP) then
    set noglob
        setenv TERMCAP ${TERMCAP}'Co#8:pa#64:op=\E[37;40m:AF=\E[3%dm:AB=\E[4%dm:'
        # for systems with terminfo, add 'colors#8, pairs#64, op=\E[37;40m, setaf=\E[3%p1%dm, setab=\E[4%p1%dm,' to the info file.  see terminfo(4), infocmp(1), and tic(1) for information on how this can be done.
    unset noglob
endif

if ($?tcsh) then
    if ( (! $?SSH_AUTH_SOCK) && (! $?SSH2_AUTH_SOCK) && (-X ssh-agent) ) then
	eval `ssh-agent -c`
	if (-r ${HOME}/.ssh/identity) then
	    ssh-add
	endif
        set ssh_agent_started
    endif
endif

if ($?tcsh) then
    bindkey	"^[[1~"	beginning-of-line		# home
    bindkey	"^[[4~"	end-of-line			# end
    bindkey	"^[[5~"	history-search-backward		# pageup
    bindkey	"^[[6~"	history-search-forward		# pagedown
    bindkey	"^B"	backward-word
    bindkey	"^F"	forward-word
    bindkey	"^[b"	backward-char
    bindkey	"^[f"	forward-char
endif

if (! $?NOMOTD && -r /etc/motd) then
    more /etc/motd
    setenv NOMOTD
endif

if (-e ${HOME}/TODO && ! -z ${HOME}/TODO && ! $?EMACS) then
    echo ""
    echo "TODO:"
    echo "-----"
    echo ""
    more ${HOME}/TODO
endif

if ($?tcsh) then
    if (-X frm) then
        echo ""
        frm -s n -s u -t -n
    endif
endif

echo ""
if ($OSTYPE == solaris) then
    quota
else
    quota -q
endif

if ($?DISPLAY && ! $?SSH_DISPLAY) then
    setenv SSH_DISPLAY $DISPLAY
endif

# application setup
setenv ENSCRIPT -G
if (! $?EDITOR) then
    setenv EDITOR emacs
endif
setenv BLOCKSIZE K
setenv CVS_RSH ssh
setenv PAGER "less -m -e -i"

# shell stuff

# set the prompt
if (! $?prompt) then
    if ($?tcsh) then
        if ($TERM == xterm) then
            set prompt='%{\e]0\;[%m %n] \!:%~>^g%}[%m %n] \!:%B%~%b%# '
        else
            set prompt='[%m %n] \!:%B%~%b%# '
        endif
    else
        set prompt='[\!] '
    endif
endif

if (! $?watch) then
    set watch=(1 $user any)
endif

unset autologout
set pushdtohome notify noclobber listjobs autolist
set cdpath=${HOME}
set history=500
set ignoreeof=3
set savehist=100
set symlinks=ignore
set time=(10 "%Us usr, %Ss kern, %Es real, %Xk shrd, %Dk usr")

if (-x ${HOME}/software/bin-${MACHTYPE}-${OSTYPE}/myfrm) then
    set tperiod=1
else
    unalias periodic
    if ($?MAIL) then
        set mail=(60 $MAIL)
    endif
endif
