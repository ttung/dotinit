set rmalias=`alias rm`
unalias rm

echo "Performing clean-up:"
    echo "  Pine debug files..."
    rm -f ~/.pine-debug* >& /dev/null
    echo "  dead letters..."
    rm -f ~/dead.letter
    echo "  x-session errors..."
    rm -f ~/.xsession-errors
    echo "  saves..."
    rm -f ~/.saves-* >& /dev/null

alias rm $rmalias
unset rmalias

stty erase '^H' kill '^U' intr '^C'

if (! $?EMACS ) then
	set noglob
	    eval `tset -s - -k -m su:\?$TERM \?$TERM` >& /dev/null
	unset noglob

	mesg y
endif

if ($?SHLVL && $?tcsh) then
    if ( ($SHLVL == 1) && (! $?SSH_AUTH_SOCK) && (-X ssh-agent) ) then
	eval `ssh-agent -c`
	if (-r ${HOME}/.ssh/identity) then
	    ssh-add
	endif
    endif
endif

if ($?tcsh) then
    bindkey	"^[[1~"	beginning-of-line		# home
    bindkey	"^[[4~"	end-of-line			# end
    bindkey	"^[[5~"	history-search-backward		# pageup
    bindkey	"^[[6~"	history-search-forward		# pagedown
    bindkey	"^B"	backward-word
    bindkey	"^F"	forward-word
    bindkey	"^[b"	backward-char
    bindkey	"^[f"	forward-char
endif

if (! $?NOMOTD && -e /etc/motd) more /etc/motd; setenv NOMOTD ""

echo ""
if ($?tcsh && -X frm) then
    frm -s n -s u -t -n
endif

if ($?DISPLAY) then
    setenv SSH_DISPLAY $DISPLAY
endif

# application setup
setenv ENSCRIPT -G
setenv EDITOR emacs
setenv BLOCKSIZE K

# shell stuff

# set the prompt
if ($?TERM) then
    if ($?tcsh) then
        if ($TERM == xterm) then
            set prompt='%{\e]0\;[%n %m] \!:%~>^g%}[%n %m] \!:%B%~%b%# '
        else
            set prompt='[%n %m] \!:%B%~%b%# '
        endif
    else
        set prompt='[\!] '
    endif
endif

unset autologout
set pushdtohome notify noclobber listjobs autolist
set cdpath=${HOME}
set history=500
set ignoreeof=3
set savehist=100
set symlinks=ignore
set time=(10 "%Us usr, %Ss kern, %Es real, %Xk shrd, %Dk usr")

if ($?tcsh && -X myfrm) then
    set tperiod=1
else
    unalias periodic
    if ($?MAIL) then
        set mail=(60 $MAIL)
    endif
endif
