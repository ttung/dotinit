echo "Performing clean-up:"
    echo "  core files..."
    rm -f ~/core
    echo "  Pine debug files..."
    rm -f ~/.pine-debug* >& /dev/null
    echo "  dead letters..."
    rm -f ~/dead.letter
    echo "  x-session errors..."
    rm -f ~/.xsession-errors
    echo "  saves..."
    rm -f ~/.saves-* >& /dev/null

stty erase '^H' kill '^U' intr '^C'

if (! $?EMACS ) then
	set noglob
	    eval `tset -s - -k -m su:\?$TERM \?$TERM` >& /dev/null
	unset noglob

	mesg y
endif

#fix ssh problems
if (`where ssh1` != "") then
	alias scp	scp1
	alias ssh	ssh1
	alias ssh-add	ssh-add1
	alias ssh-agent	ssh-agent1
	alias ssh-askpass	ssh-askpass1
	alias ssh-keygen	ssh-keygen1
endif

if ($?SHLVL) then
    if ( ($SHLVL == 1) && (! $?SSH_AUTH_SOCK) ) then
	eval `ssh-agent -c`
	if (-r ${HOME}/.ssh/identity) then
	    ssh-add
	endif
    endif
endif

if ($?tcsh) then
    bindkey "^[[1~" beginning-of-line        # home
    bindkey "^[[4~" end-of-line              # end
    bindkey "^[[5~" history-search-backward  # pageup
    bindkey "^[[6~" history-search-forward   # pagedown
endif

if (! $?NOMOTD && -e /etc/motd) more /etc/motd; setenv NOMOTD ""

echo ""
frm -s new -t -n

if ($?DISPLAY) then
	setenv SSH_DISPLAY $DISPLAY
endif

# application setup
setenv ENSCRIPT -G
setenv EDITOR emacs
setenv BLOCKSIZE K

# shell stuff

# set the prompt
if ($?TERM) then
    if ($?tcsh) then
	if ($TERM == xterm) then
	    set prompt='%{\e]0\;[%n %m] \!:%~>^g%}[%n %m] \!:%B%~%b%# '
	else
	    set prompt='[%n %m] \!:%B%~%b%# '
    	endif
    else
	set prompt='[\!] '
    endif
endif

set notify pushdtohome ignoreeof notify noclobber listjobs autolist
unset autologout
set history=500
set savehist=100
set cdpath=${HOME}
set echo_style=both
set symlinks=ignore
set time=(10 "%Us usr, %Ss kern, %Es real, %Xk shrd, %Dk usr")
if (! -e ${HOME}/software/bin-${MACHTYPE}-${OSTYPE}/myfrm) then
	set tperiod=1
endif
